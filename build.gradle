subprojects {
  apply plugin: 'java'
  apply plugin: 'checkstyle'
  apply plugin: 'findbugs'
  
  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'

  repositories {
    jcenter()
  }
  
  task wrapper(type: Wrapper) {
    gradleVersion = '4.3'
  }

  dependencies {
      testCompile 'junit:junit:4.12'
      testCompile 'org.hamcrest:hamcrest-all:1.3'
  }

  sourceSets {
    acceptanceTest {
      java {
        srcDir file('../acceptanceTests/src')
        compileClasspath += main.output
        runtimeClasspath += main.output
      }
    }
  }

  configurations {
    acceptanceTestCompile.extendsFrom testCompile
    acceptanceTestRuntime.extendsFrom testRuntime
  }

  task acceptanceTest(type: Test) {
    testClassesDir = project.sourceSets.acceptanceTest.output.classesDir
    classpath = project.sourceSets.acceptanceTest.runtimeClasspath
  }
  
  checkstyle {
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    checkstyleTest.enabled = true
    checkstyleAcceptanceTest.enabled = false
    toolVersion = '7.1.2'
  }
  
  tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
  }
}
